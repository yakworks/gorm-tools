//It all starts here
buildscript {
  repositories {
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.grails.plugins:hibernate5:$vGormHibernate"
    classpath "org.grails.plugins:views-gradle:$vViews"
    classpath "org.yakworks:gradle-plugins:$vShipyak"
  }
}

group = "org.yakworks"

apply plugin: 'yakworks.shipyak' //our opinionated defaults for gradle groovy & grails projects

subprojects {
  repositories {
    maven { url "https://repo.grails.org/grails/core" }
  }

  ['org.grails.grails-plugin', 'org.grails.grails-web'].each { plugId -> plugins.withId(plugId) {

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
      developmentOnly("org.springframework.boot:spring-boot-devtools")
      compileOnly "io.micronaut:micronaut-inject-groovy"
      implementation "org.springframework.boot:spring-boot-starter-logging"
      implementation "org.springframework.boot:spring-boot-autoconfigure"
      implementation "org.grails:grails-core"
      implementation "org.springframework.boot:spring-boot-starter-tomcat"

      implementation "org.grails:grails-web-boot"

      implementation "org.grails:grails-logging"
      implementation "org.grails:grails-plugin-databinding"
      implementation "org.grails:grails-plugin-services"

      implementation "org.grails.plugins:cache"
      implementation "org.grails.plugins:events"

      implementation "org.grails.plugins:hibernate5"
      implementation "org.hibernate:hibernate-core:$vHibernate"
      implementation "org.hibernate:hibernate-ehcache:$vHibernate"

      implementation 'org.codehaus.groovy:groovy-dateutil'

      implementation "dk.glasius:external-config:$vExternalConfig"

      // compile "io.activej:activej-http:5.0-rc3"
      // compile "io.activej:activej-launchers-common:5.0-rc3"
      implementation "io.activej:activej-launchers-http:5.0-rc3"
      implementation "io.activej:activej-boot:5.0-rc3"
      implementation "io.activej:activej-promise:5.0-rc3"

      // *** compileOnly, dependent projects need to add these in ***

      //used for json parsing but require it to be added to project that uses this
      compileOnly("org.grails.plugins:views-json:$vViews") {
        //exclude controllers since it messes with the domain compile and map constructor
        exclude group:"org.grails", module: "grails-plugin-controllers"
      }
      compileOnly "org.grails.plugins:views-json-templates:$vViews"
      //*** End Compile Only ****

      runtimeOnly "com.h2database:h2"
      runtimeOnly "com.zaxxer:HikariCP:$vHikari"

      testCompile "org.mockito:mockito-core"
      testCompile "org.grails:grails-gorm-testing-support"
      testCompile "org.grails:grails-web-testing-support"
      testCompile "org.grails.plugins:build-test-data:$vBuildTestData"
      testCompile "org.grails:views-json-testing-support:$vViews"

    }

    // Defaults from https://github.com/radarsh/gradle-test-logger-plugin
    testlogger {
      // theme 'standard'
      showExceptions true
      showStandardStreams = true //shows the printlns in console
      showPassed false
      showSkipped true
      showFailed true
      // slowThreshold 2000 //if tests take longer than this will show time it took
      // showSummary true
      // showSimpleNames false
      // showPassed true
      // showSkipped true
      // showFailed true
      // showPassedStandardStreams true
      // showSkippedStandardStreams true
      // showFailedStandardStreams true
    }
  }} //end of plugins.withId iteration

  configurations {
    testCompile.extendsFrom compileOnly //includes the compileOnly in tests too so we don't have to list twice
    testCompile.extendsFrom implementation
    all {
      resolutionStrategy.cacheChangingModulesFor 1, 'seconds' //when changing = true this sets the cache time
    }
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
  }

  tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
      forkOptions.jvmArgs = ['-Xmx1024m', '-Dgradle.projectDir=' + project.projectDir.absolutePath]
    }
  }

  // sets the path to sys properties so we can use it find grails-app/conf during AST
  // plugins.withId('groovy') {
  //   compileGroovy {
  //     groovyOptions.fork = true
  //     groovyOptions.forkOptions.jvmArgs = ['-Dgradle.projectDir=' + project.projectDir.absolutePath]
  //   }
  // }
  tasks.withType(Test) {
    //so that we can use in config and share resources/rootlocation across projects for testing
    systemProperty "gradle.rootProjectDir", rootProject.projectDir.absolutePath
    systemProperty "gradle.projectDir", project.projectDir.absolutePath
  }
}

ext.codenarcRuleset= '''
  // getRule('Println').enabled = false
  // getRule('AbcMetric').enabled = false
'''
