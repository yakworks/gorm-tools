apply plugin: "yakworks.grails-plugin"

apply from: "${rootProject.projectDir}/gradle/test-integration.gradle"

//shared resources to pick up the Gorm defaults from application.groovy, as well as simple logback
sourceSets.test.resources.srcDirs += [project(':gorm-test-support').sourceSets.main.resources]

dependencies {

  console "org.grails:grails-console"
  profile "org.grails.profiles:rest-api"

  api project(":gorm-tools")
  api project(":gorm-security")
  api project(":gorm-etl")
  api "org.yakworks:grails-kit:$vYakGrailsKit"
  api "org.yakworks:grails-external-config:$vYakGrailsKit"
  //api "dk.glasius:external-config:$vExternalConfig"
  api 'jakarta.annotation:jakarta.annotation-api:2.1.0'

  // for the mock and seed data
  compileOnly("io.github.longwa:build-test-data:$vBuildTestData"){
    exclude group:"org.grails", module: "grails-gorm-testing-support"
  }

  implementation "org.springframework.boot:spring-boot-starter-logging"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.grails:grails-core"
  implementation "org.springframework.boot:spring-boot-starter-tomcat"

  implementation "org.grails:grails-web-boot"
  implementation "org.grails:grails-plugin-services"
}

bootRun {
  ignoreExitValue true
  jvmArgs(
    '-Dspring.output.ansi.enabled=always',
    '-noverify',
    '-XX:TieredStopAtLevel=1',
    '-Xmx1024m',
    '--illegal-access=permit', '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED'
  )

  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

//BootStrap fires for some reason in 4.x so exclude it
// jar {
//   exclude('yakworks/rally/BootStrap.class')
//   exclude('yakworks/rally/BootStrap$*.class')
// }

// sourceSets.main.resources.srcDirs += "src/main/groovy"

// task printSourceSetInformation(){
//   doLast{
//     println "["+sourceSets.main.name+"]"
//     println "${sourceSets.main.resources.srcDirs}"
//     // sourceSets.each { srcSet ->
//     //   println "["+srcSet.name+"]"
//     //   print "-->Source directories: "+srcSet.allResources.srcDirs+"\n"
//     //   print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
//     //   println ""
//     // }
//   }
// }



tasks.register("verifyNoSnapshots") {
  group = "verification"
  description = "Check whether there are any SNAPSHOT dependencies."
  doLast {
    def snapshotsList = project.configurations
      .findAll { it.name == "compileClasspath" || it.name == "runtimeClasspath" }
      .findAll { it.canBeResolved }
      .resolvedConfiguration.resolvedArtifacts.flatten()
      .findAll { it.moduleVersion.id.version.endsWith('-SNAPSHOT') }.unique()
    // println "checkSnapshots"
    // println snapshotsList
    if (!snapshotsList.isEmpty()) {
      throw new GradleException("Please get rid of snapshots for following dependencies before releasing $snapshotsList")
    }
  }
}
