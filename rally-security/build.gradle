apply plugin: "yakworks.grails-plugin"

repositories {
  maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" } //for pac4j deps
}

dependencies {

  console "org.grails:grails-console"
  profile "org.grails.profiles:rest-api"

  compile project(":rally-domain")

  api("org.grails.plugins:spring-security-rest:$vSpringSecurityRest"){
    exclude group: 'net.minidev', module: 'json-smart'
  }
  api "org.pac4j:pac4j-oidc:3.8.3"
  api "org.pac4j:pac4j-saml:3.8.3"

  //bump to deal with vulnerability in one above and was not downloading
  api "net.minidev:json-smart:$vJsonSmart"

  ['aspectj', 'core', 'spring', 'web', 'hazelcast'].each {
    api "org.apache.shiro:shiro-$it:$vShiro"
  }

  compileOnly("org.grails:grails-web-mvc")
  //for AppResourceLoader in AttachmentSupport
  compileOnly "org.grails.plugins:view-tools:$vViewTools"

  implementation "org.springframework.boot:spring-boot-starter-logging"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.grails:grails-core"
  implementation "org.springframework.boot:spring-boot-starter-tomcat"

  implementation "org.grails:grails-web-boot"
  implementation "org.grails:grails-plugin-services"

  runtime "org.grails.plugins:external-config:$vExternalConfig"

  testRuntime "org.grails.plugins:external-config:$vExternalConfig"
  testRuntime "com.h2database:h2"
  testRuntime "javax.xml.bind:jaxb-api:2.3.1"
  testRuntime "com.zaxxer:HikariCP:$vHikari"

}

bootRun {
  ignoreExitValue true
  jvmArgs(
    '-Dspring.output.ansi.enabled=always',
    '-noverify',
    '-XX:TieredStopAtLevel=1',
    '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

//BootStrap fires for some reason in 4.x so exclude it
jar {
  exclude('yakworks/security/BootStrap.class')
  exclude('yakworks/security/BootStrap$*.class')
}

integrationTest {
  testlogger {
    showPassed false
    showExceptions true
    showStandardStreams = true //shows the printlns in console
    showPassed false
    showSkipped true
    showFailed true
    showSummary true
  }
}
