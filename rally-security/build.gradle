apply plugin: "yakworks.grails-plugin"

repositories {
  maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" } //for pac4j deps
}

//shared resources to pick up the Gorm defaults from application.groovy, as well as simple logback
sourceSets.test.resources.srcDirs += [project(':gorm-test-support').sourceSets.main.resources]

dependencies {

  console "org.grails:grails-console"
  profile "org.grails.profiles:rest-api"

  api project(":rally-domain")

  api("org.grails.plugins:spring-security-rest:$vSpringSecurityRest"){
    exclude group: 'net.minidev', module: 'json-smart'
  }
  //bump to deal with vulnerability in one above and was not downloading
  api "net.minidev:json-smart:$vJsonSmart"

  api "org.pac4j:pac4j-oidc:3.8.3"
  api("org.pac4j:pac4j-saml:3.8.3"){
    // fix hazel cast Not supported: "http://javax.xml.XMLConstants/property/accessExternalDTD"
    exclude module: 'xalan'
    exclude module: 'org.hibernate'
  }
  // this is here beacause the org.opensaml:opensaml-storage-impl in pac4j deps depends on it
  api("org.hibernate:hibernate-entitymanager:$vHibernate")

  ['aspectj', 'core', 'spring', 'web'].each {
    api "org.apache.shiro:shiro-$it:$vShiro"
  }

  // api("org.apache.shiro:shiro-hazelcast:$vShiro"){
  //   exclude group: 'com.hazelcast', module: 'hazelcast'
  // }

  compileOnly("org.grails:grails-web-mvc")

  implementation "org.springframework.boot:spring-boot-starter-logging"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.grails:grails-core"
  implementation "org.springframework.boot:spring-boot-starter-tomcat"

  implementation "org.grails:grails-web-boot"
  implementation "org.grails:grails-plugin-services"

  testRuntimeOnly "com.h2database:h2:$vH2"
  testRuntimeOnly "javax.xml.bind:jaxb-api:2.3.1"
  testRuntimeOnly "com.zaxxer:HikariCP:$vHikari"

}

bootRun {
  ignoreExitValue true
  jvmArgs(
    '-Dspring.output.ansi.enabled=always',
    '-noverify',
    '-XX:TieredStopAtLevel=1',
    '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

//BootStrap fires for some reason in 4.x so exclude it
jar {
  exclude('yakworks/security/BootStrap.class')
  exclude('yakworks/security/BootStrap$*.class')
}

integrationTest {
  testlogger {
    showPassed false
    showExceptions true
    showStandardStreams = true //shows the printlns in console
    showPassed false
    showSkipped true
    showFailed true
    showSummary true
  }
}
